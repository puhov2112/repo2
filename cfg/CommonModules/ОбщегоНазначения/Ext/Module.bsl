
Функция ЗначенияСвойствОбъекта(Ссылка, Реквизиты) Экспорт
	
	ЗначенияСвойствОбъекта = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	&Таблица КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ЗаменяемыеПоля = СтрРазделить(Реквизиты, ",");
	Для й = 0 По ЗаменяемыеПоля.ВГраница() Цикл
		
		ОчередноеПоле = ЗаменяемыеПоля[й];
		ОчередноеПоле = СокрЛП(ОчередноеПоле);
		ЗначенияСвойствОбъекта.Вставить(ОчередноеПоле);
		
		ЗаменяемыеПоля[й] = СтрШаблон("%1 КАК %1", ОчередноеПоле);
		
	КонецЦикла;
	ЗаменяемыеПоля = СтрСоединить(ЗаменяемыеПоля, "," + Символы.ПС);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИСТИНА КАК Поле1", ЗаменяемыеПоля);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", Ссылка.Метаданные().ПолноеИмя());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(ЗначенияСвойствОбъекта, Выборка);
		
	КонецЕсли;
	
	Возврат ЗначенияСвойствОбъекта;
	
КонецФункции

Функция ЗначениеСвойстваОбъекта(Ссылка, Реквизит) Экспорт
	
	ЗначенияСвойствОбъекта = ЗначенияСвойствОбъекта(Ссылка, Реквизит);
	ЗначениеСвойстваОбъекта = ЗначенияСвойствОбъекта[Реквизит];
	
	Возврат ЗначениеСвойстваОбъекта;
	
КонецФункции
